//Title:	dwDistributeRotations.mel
//Author:	David Walden
//		www.davidwalden.com
//		dwalden74@hotmail.com
//
//Description: Used to distribute bone rotations among many joints. For example, distributes shoulder´s 
//		x-rotation among as many "bicep" joints as desired.

global proc dwDistributeRotations (string $incomingObject,
					string $incomingAttr,
					string $outputObjects[],
					string $outputAttr,
					string $multiDivideName)
{
if (`objExists $multiDivideName`)
	error ($multiDivideName + " already exists in scene.\n");

if (! `objExists $multiDivideName`)
	{
	//Create multiplyDivide node and set operation to "divide".
	shadingNode -asUtility -n $multiDivideName multiplyDivide;
	setAttr ($multiDivideName + ".operation") 2;
	}
	
//Connect incomingObject.incomingAttr to multidivide input1X.
connectAttr -f ($incomingObject + "." + $incomingAttr) ($multiDivideName + ".input1X");

//Get size of $outputObjects[] array.
int $sizeOutput = `size($outputObjects)`;

//Assign number by which incomingObject.incomingAttr value will be divided (size of $outputObjects).
setAttr ($multiDivideName + ".input2X") $sizeOutput;

//Connect output to all objects in $outputObjects array.
for ($obj in $outputObjects)
	{
	connectAttr -f ($multiDivideName + ".outputX") ($obj + "." + $outputAttr);
	}
}//End of procedure.