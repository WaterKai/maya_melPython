//Title:	dwRTInsertSingleJoint.mel
//Author:	David Walden
//		www.davidwalden.com
//		dwalden74@hotmail.com
//
//Updated: 	April 1, 2004: Added the $startJoint argument, which will allow user to insert a joint after any inputted joint
//				(not only after the current selection).
//
//Procedure for inserting a single joint.
global proc dwRTInsertSingleJoint(string $startJoint, float $position)
{
string $endJoint[] = `listRelatives -path -c -type joint $startJoint`;

if (! `size $startJoint`)
	error "No joint specified. Try again.";
if (! `size $endJoint`)
	error "Joint has no child joints. Try again.";
if (`size $endJoint` > 1)
	error "Joint has multiple child joints. This causes unexpected results. Select a joint with one child joint and try again.";	

//pickWalk -d down;
//string $endJoint[] = `ls -sl -type joint`;

float $posA[] = `xform -q -ws -t $startJoint`;
float $posB[] = `xform -q -ws -t $endJoint[0]`;

vector $vectorA = <<$posA[0], $posA[1], $posA[2]>>;
vector $vectorB = <<$posB[0], $posB[1], $posB[2]>>;

vector $lengthVector = ($vectorB - $vectorA) * $position;
vector $startVector = $lengthVector + $vectorA;

string $newJoint = `insertJoint $startJoint`;

move -a ($startVector.x) ($startVector.y) ($startVector.z) ($newJoint + ".rotatePivot") ($newJoint + ".scalePivot");
}


