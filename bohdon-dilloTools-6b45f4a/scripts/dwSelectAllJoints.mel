//Title:	dwSelectAllJoints.mel
//Author:	David Walden
//		www.davidwalden.com
//		dwalden74@hotmail.com

//Date: Jan. 2003
//Description: Selects every joint object below and including the currently selected joint. The resulting
//string array is ordered such that the first joint selected is index 0, and closer children come before
//grandchildren (opposite order of what listRelatives returns, which returns granchildren before children).

global proc dwSelectAllJoints()
{
string $rootSelection[] = `ls -sl -type "joint"`;
if (size($rootSelection) == 0)
	error "Select the root joint and try again.";
if (size($rootSelection) > 1)
	error "Select only one joint and try again.";

string $childJointsInHierarchy[] = `listRelatives -type "joint" -ad -pa $rootSelection[0]`;
int $sizeChildren = `size($childJointsInHierarchy)`;

string $allJoints[];
$allJoints[0] = $rootSelection[0];

int $i;
int $n = $sizeChildren;
for ($i = 1; $i < ($sizeChildren + 1); $i++)
	{
	$allJoints[$i] = $childJointsInHierarchy[$n - 1];
	$n--;
	}

select -r $allJoints;
}